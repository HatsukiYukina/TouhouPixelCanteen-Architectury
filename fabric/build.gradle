plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    maven {
        name = 'curse.maven'
        url = "https://cursemaven.com"
    }
    maven { url "https://maven.terraformersmc.com/releases/" } //modmenu
    maven { url "https://maven.shedaniel.me/" } //cloth config api
    maven { url 'https://maven.nucleoid.xyz' }

}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    modImplementation "curse.maven:farmers-delight-refabricated-993166:7199751"

    modImplementation "eu.pb4:polymer-core:0.9.19+1.21.1" //polymer

    modApi "com.terraformersmc:modmenu:11.0.1" //modmenu
    modApi "me.shedaniel.cloth:cloth-config-fabric:15.0.140" //cloth-config

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}


processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
